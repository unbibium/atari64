
MOVFR	LDA RESHO
	STA FACHO
	LDA RESMOH
	STA FACMOH
	LDA RESMO
	STA FACMO
	LDA RESLO
	STA FACLO
	JMP NORMAL
MOVFM	STA INDEX1
	STY INDEX1+1
	LDY #3+ADDPRC
	LDA (INDEX1),Y
	STA FACLO
	DEY
	LDA (INDEX1),Y
	STA FACMO
	DEY
	LDA (INDEX1),Y
	STA FACMOH
	DEY
	LDA (INDEX1),Y
	STA FACSGN
	ORA #0200
	STA FACHO
	DEY
	LDA (INDEX1),Y
	STA FACEXP
	STY FACOV
	RTS   
MOV2F	LDX #TEMPF2
	.BYTE $2C
MOV1F	LDX #TEMPF1
	LDY #0
	BEQ MOVMF
MOVVF	LDX FORPNT
	LDY FORPNT+1
MOVMF	JSR ROUND
	STX INDEX1
	STY INDEX1+1
	LDY #3+ADDPRC
	LDA FACLO
	STA (INDEX),Y
	DEY
	LDA FACMO
	STA (INDEX),Y
	DEY
	LDA FACMOH
	STA (INDEX),Y
	DEY
	LDA FACSGN
	ORA #0177
	AND FACHO
	STA (INDEX),Y
	DEY
	LDA FACEXP
	STA (INDEX),Y
	STY FACOV
	RTS
MOVFA	LDA ARGSGN
MOVFA1	STA FACSGN
	LDX #4+ADDPRC
MOVFAL	LDA ARGEXP-1,X
	STA FACEXP-1,X
	DEX
	BNE MOVFAL
	STX FACOV
	RTS
MOVAF	JSR ROUND
MOVEF	LDX #5+ADDPRC
MOVAFL	LDA FACEXP-1,X
	STA ARGEXP-1,X
	DEX
	BNE MOVAFL
	STX FACOV
MOVRTS	RTS
ROUND	LDA FACEXP
	BEQ MOVRTS
	ASL FACOV
	BCC MOVRTS
INCRND	JSR INCFAC
	BNE MOVRTS
	JMP RNDSHF
SIGN	LDA FACEXP
	BEQ SIGNRT
FCSIGN	LDA FACSGN
FCOMPS	ROL
	LDA #$FF
	BCS SIGNRT
	LDA #1
SIGNRT	RTS
SGN	JSR SIGN
FLOAT	STA FACHO
	LDA #0
	STA FACHO+1
	LDX #0210
FLOATS	LDA FACHO
	EOR #0377
	ROL
FLOATC	LDA #0
	STA FACLO
	STA FACMO
FLOATB	STX FACEXP
	STA FACOV
	STA FACSGN
	JMP FADFLT
ABS	LSR FACSGN
	RTS
FCOMP	STA INDEX2
FCOMPN	STY INDEX2+1
	LDY #0
	LDA (INDEX2),Y
	INY
	TAX
	BEQ SIGN
	LDA (INDEX2),Y
	EOR FACSGN
	BMI FCSIGN
	CPX FACEXP
	BNE FCOMPC
	LDA (INDEX2),Y
	ORA #0200
	CMP FACHO
	BNE FCOMPC
	INY
	LDA (INDEX2),Y
	CMP FACMOH
	BNE FCOMPC
	INY
	LDA (INDEX2),Y
	CMP FACMO
	BNE FCOMPC
	INY
	LDA #0177
	CMP FACOV
	LDA (INDEX2),Y
	SBC FACLO
	BEQ QINTRT
FCOMPC	LDA FACSGN
	BCC FCOMPD
	EOR #0377
FCOMPD	JMP FCOMPS

